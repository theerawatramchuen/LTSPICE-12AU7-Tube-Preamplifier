*Vacuum tube triode library 11.2020 Fesz
*based on the mathematical model of Norman Koren
*
.subckt 6S19P 1 2 3
xU1 1 2 3 VTT Cak=0.9p Cga=2.4p Cgk= 2.3p Kg1=2500 ex=2 mu=2.6 kp=13 kvb=2000 vgc=-3 Rgk=2k
.ends
*
.subckt 12AX7 1 2 3
xU1 1 2 3 VTT Cak=0.33p Cga=1.6p Cgk= 1.65p Kg1=1060 ex=1.4 mu=100 kp=600 kvb=300 vgc=0 Rgk=2k
.ends
*
.subckt 12AU7 1 2 3
xU1 1 2 3 VTT Cak=0.24p Cga=1.4p Cgk= 1.6p Kg1=920 ex=1.3 mu=17 kp=330 kvb=300 vgc=0 Rgk=2k
.ends
*
*vacuum tube triode model
*K - cathode
*A - Anode
*G - Grid
.subckt VTT A G K
C1 A G {Cga}
C2 G K {Cgk}
B1 A K I=(pow(v(e1),ex)/kg1)*(v(e1)+abs(V(e1)))/V(e1)
C3 A K {Cak}
D1 G K DGK
B2 e1 0 V=(V(a,k)/Kp)*Log(1+exp(Kp*(1/mu+(v(g,k)+vgc)/sqrt(kvb+pow(V(a,k),2)))))
.model DGK D(Vfwd=0 Ron={RGK} Roff=100meg)
.ends 